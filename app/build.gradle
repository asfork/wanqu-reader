import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.0 rc2"

    defaultConfig {
        applicationId "com.steve.wanqureader"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "6.5.7"
        manifestPlaceholders = [BUGLY_CHANNEL_VALUE: "github"]
        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    buildTypes {
        debug{
            manifestPlaceholders = [BUGLY_ENABLE_DEBUG: "true"]
        }
        release {
            minifyEnabled false
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [BUGLY_ENABLE_DEBUG: "false"]
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为 wanqu_v1.0_wandoujia.apk
                        def fileName = "wanqu_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
    productFlavors {
        github {}
        wandoujia {}
        coolapk {}
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [BUGLY_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:customtabs:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'

    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'

    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.google.dagger:dagger:2.4'
    compile 'com.github.satyan:sugar:1.5'
    compile 'com.stephentuso:welcome:0.6.1'

    compile 'com.tencent.bugly:crashreport:latest.release'

    compile 'com.github.jd-alexander:LikeButton:0.2.0'
    compile 'com.github.javiersantos:AppUpdater:2.1'
}

// about.html is templatized so that we can automatically insert
// our version string in to it at build time.
task filterAbout << {
    copy {
        from "src/main/templates/about.html"
        into "src/main/assets"
        filter(ReplaceTokens, tokens: [versionname: android.defaultConfig.versionName])
    }
}

preBuild.dependsOn filterAbout